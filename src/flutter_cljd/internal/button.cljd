(ns ^:internal flutter-cljd.internal.button
  (:require
   ["dart:core" :as core]
   ["package:flutter/foundation.dart" :as f]
   ["package:flutter/widgets.dart" :as w]
   ["package:flutter/services.dart" :as s]
   [flutter-cljd.core :as c]
   [flutter-cljd.types :as t]
   [flutter-cljd.internal.with-inherited :as wi]
   [flutter-cljd.internal.builder :as bd]
   [flutter-cljd.internal.animated :as an]
   [flutter-cljd.inherited-keys :as inh]))

(declare ^:private ButtonState)

;; Button widget
(deftype Button [key child is-enabled on-tap on-tap-down on-tap-up
                 on-tap-cancel on-double-tap on-long-press on-secondary-tap
                 on-secondary-tap-up on-secondary-tap-down on-secondary-tap-cancel
                 on-highlight-changed on-hover mouse-cursor enable-feedback
                 exclude-from-semantics focus-node can-request-focus
                 on-focus-change autofocus]
  :extends (w/StatefulWidget .key key)

  (createState [_] (ButtonState false false nil nil nil nil nil #{}))

  (debugFillProperties [this properties]
    (.debugFillProperties ^super this properties)
    (let [gestures (cond-> []
                     on-tap (conj "tap")
                     on-double-tap (conj "double tap")
                     on-long-press (conj "long press")
                     on-tap-down (conj "tap down")
                     on-tap-up (conj "tap up")
                     on-tap-cancel (conj "tap cancel")
                     on-secondary-tap (conj "secondary tap")
                     on-secondary-tap-up (conj "secondary tap up")
                     on-secondary-tap-down (conj "secondary tap down")
                     on-secondary-tap-cancel (conj "secondary tap cancel"))]
      (.add properties (#/(f/IterableProperty String) "gestures" gestures .ifEmpty "<none>"))
      (.add properties (#/(f/DiagnosticsProperty s/MouseCursor) "mouseCursor" mouse-cursor)))))

(defn- update-states
  ([this value add]
   (let [notifier (.-internalStatesController this)
         current (.-value notifier)]
     (set! (.-oldStates this) current)
     (if add
       (set! (.-value notifier) (conj current value))
       (set! (.-value notifier) (disj current value))))))

(defn- activate-on-intent [this _]
  (when-let [on-tap (.-on-tap (.-widget this))]
    (when (and () (.-enable-feedback (.-widget this)))
      (w/Feedback.forTap (.-context this)))
    (on-tap)))

(defn- states-controller [this]
  (.-internalStatesController this))

(defn- primary-button-enabled? [_ ^Button widget]
  (and (.-is-enabled widget)
       (or (.-on-tap widget)
           (.-on-double-tap widget)
           (.-on-long-press widget)
           (.-on-tap-up widget)
           (.-on-tap-down widget))))

(defn- secondary-button-enabled? [_ ^Button widget]
  (and (.-is-enabled widget)
       (or (.-on-secondary-tap widget)
           (.-on-secondary-tap-up widget)
           (.-on-secondary-tap-down widget))))

(defn- widget-enabled? [this ^Button widget]
  (or (primary-button-enabled? this widget)
      (secondary-button-enabled? this widget)))

(defn- enabled? [this]
  (widget-enabled? this (.-widget this)))

(defn- handle-states-controller-change [_]
  nil)

(defn- init-states-controller [this]
  (set! (.-internalStatesController this) (w/ValueNotifier #{}))
  (let [controller (states-controller this)]
    (update-states this :disabled (not (enabled? this)))
    (.addListener controller (.-handleStatesControllerChange this))))

(defn- update-highlight
  ([this type value]
   (update-highlight this type value true))
  ([this type value call-on-hover]
   ;; Update states
   (condp = type
     :pressed
     (update-states this :pressed value)

     :hovered
     (when call-on-hover
       (update-states this :hovered value))

     nil)

   ;; Call event handlers
   (condp = type
     :pressed
     (when-let [on-change (.-on-highlight-changed (.-widget this))]
       (on-change value))

     :hovered
     (when (and call-on-hover
                (.-on-hover (.-widget this)))
       ((.-on-hover (.-widget this)) value))

     nil)))

(defn- should-show-focus? [this]
  (condp = (w/MediaQuery.maybeNavigationModeOf (.-context this))
    w/NavigationMode.directional (.-hasFocus this)
    (and (enabled? this) (.-hasFocus this))))

(defn- update-focus-highlights [this]
  (let [show-focus (condp = (.-highlightMode (.-instance w/FocusManager))
                     w/FocusHighlightMode.traditional (should-show-focus? this)
                     false)]
    (update-highlight this :focused show-focus)))

(defn- clear-offsets [this]
  (set! (.-cursorLocalOffset this) nil)
  (set! (.-cursorGlobalOffset this) nil))

;; Focus handling

(defn- handle-focus-update [this hasFocus]
  (set! (.-hasFocus this) hasFocus)
  ; Set directly rather than via updateHighlight because this widget's states 
  ; include focused if it has focus, rather than if showing focus per FocusManager
  (update-states this :focused hasFocus)
  (update-focus-highlights this)
  (when-let [on-change (.-on-focus-change (.-widget this))]
    (on-change hasFocus)))

;; Tap handling
(defn- handle-any-tap-down [this details]
  (set! (.-cursorGlobalOffset this) (.-globalPosition details))
  (set! (.-cursorLocalOffset this) (.-localPosition details))
  (update-states this :pressed true)
  ;; (.updateKeepAlive this)
  (update-highlight this :pressed true))

(defn- handle-tap-down [this details]
  (handle-any-tap-down this details)
  (when-let [on-tap-down (.-on-tap-down (.-widget this))]
    (on-tap-down details)))

(defn- handle-tap-up [this details]
  (clear-offsets this)
  (when-let [on-tap-up (.-on-tap-up (.-widget this))]
    (on-tap-up details)))

(defn- handle-secondary-tap-down [this details]
  (handle-any-tap-down this details)
  (when-let [on-secondary-tap-down (.-on-secondary-tap-down (.-widget this))]
    (on-secondary-tap-down details)))

(defn- handle-secondary-tap-up [this details]
  (clear-offsets this)
  (when-let [on-secondary-tap-up (.-on-secondary-tap-up (.-widget this))]
    (on-secondary-tap-up details)))

(defn- handle-tap [this]
  (update-highlight this :pressed false)
  (when-let [on-tap (.-on-tap (.-widget this))]
    (when (.-enable-feedback (.-widget this))
      (w/Feedback.forTap (.-context this)))
    (on-tap)))

(defn- handle-tap-cancel [this]
  (clear-offsets this)
  (when-let [on-tap-cancel (.-on-tap-cancel (.-widget this))]
    (on-tap-cancel))
  (update-highlight this :pressed false))

(defn- handle-double-tap [this]
  (update-highlight this :pressed false)
  (when-let [on-double-tap (.-on-double-tap (.-widget this))]
    (on-double-tap)))

(defn- handle-long-press-start [this details]
  (set! (.-cursorGlobalOffset this) (.-globalPosition details))
  (set! (.-cursorLocalOffset this) (.-localPosition details)))

(defn- handle-long-press [this]
  (when-let [on-long-press (.-on-long-press (.-widget this))]
    (when (.-enable-feedback (.-widget this))
      (w/Feedback.forLongPress (.-context this)))
    (on-long-press)))

(defn- handle-secondary-tap [this]
  (update-highlight this :pressed false)
  (when-let [on-secondary-tap (.-on-secondary-tap (.-widget this))]
    (on-secondary-tap)))

(defn- handle-secondary-tap-cancel [this]
  (clear-offsets this)
  (when-let [on-secondary-tap-cancel (.-on-secondary-tap-cancel (.-widget this))]
    (on-secondary-tap-cancel))
  (update-highlight this :pressed false))

(defn- handle-hover-change [this]
  (update-highlight this :hovered (.-hovering this)))

;; Mouse handling
(defn- handle-mouse-enter [this event]
  (set! (.-hovering this) true)
  (set! (.-cursorGlobalOffset this) (.-position event))
  (set! (.-cursorLocalOffset this) (.-localPosition event))
  (when (enabled? this)
    (handle-hover-change this)))

(defn- handle-mouse-move [this event]
  (set! (.-cursorGlobalOffset this) (.-position event))
  (set! (.-cursorLocalOffset this) (.-localPosition event)))

(defn- handle-mouse-exit [this _]
  (set! (.-hovering this) false)
  (clear-offsets this)
  ; Still update highlights and run onHover even if disabled
  (handle-hover-change this))

(defn- can-request-focus? [this]
  (condp = (w/MediaQuery.maybeNavigationModeOf (.-context this))
    w/NavigationMode.directional true
    (and (enabled? this) (.-can-request-focus (.-widget this)))))

(defn- handle-focus-highlight-mode-change [this _]
  (when (.-mounted this)
    (.setState this (fn []
                      (update-focus-highlights this)))))

;; Button State
(deftype ^:private ButtonState [^:mutable hovering
                                ^:mutable hasFocus
                                ^:mutable cursorLocalOffset
                                ^:mutable cursorGlobalOffset
                                ^:mutable internalStatesController
                                ^:mutable handleFocusHighlightModeChange
                                ^:mutable handleStatesControllerChange
                                ^:mutable oldStates]
  :extends (#/(w/State Button))

  (initState [this]
    (.initState ^super this)
    (let [weak-this (core/WeakReference this)]
      (.-handleFocusHighlightModeChange! this #(handle-focus-highlight-mode-change (.-target weak-this) %))
      (.-handleStatesControllerChange! this #(handle-states-controller-change (.-target weak-this))))
    (init-states-controller this)
    (.addHighlightModeListener (.-instance w/FocusManager)
                               (.-handleFocusHighlightModeChange this)))

  (didUpdateWidget [this old-widget]
    (.didUpdateWidget ^super this old-widget)

  ;; Handle enabled state changes
    (when (not= (enabled? this) (widget-enabled? this old-widget))
      (update-states this :disabled (not (enabled? this)))

      (when-not (enabled? this)
        (update-states this :pressed false))

    ;; Don't call widget.onHover because many widgets, including button
    ;; widgets, apply setState to an ancestor context from onHover
      (update-highlight this
                        :hovered
                        (.-hovering this)
                        false))

    (update-focus-highlights this))

  (dispose [this]
    (.removeHighlightModeListener (.-instance w/FocusManager)
                                  (.-handleFocusHighlightModeChange this))
    (.removeListener (states-controller this) (.-handleStatesControllerChange this))
    (when-let [controller (.-internalStatesController this)]
      (.dispose controller))
    (.dispose ^super this))

  ;; Build method
  (build [this _context]
    (let [^Button widget (.-widget this)
          primary-enabled? (primary-button-enabled? this widget)
          secondary-enabled? (secondary-button-enabled? this widget)
          action-map {w/ActivateIntent (w/CallbackAction.
                                        .onInvoke #(activate-on-intent this %))
                      w/ButtonActivateIntent (w/CallbackAction.
                                              .onInvoke #(activate-on-intent this %))}]
      (w/Actions.
       .actions action-map
       .child
       (w/Focus.
        .focusNode (.-focus-node widget)
        .canRequestFocus (can-request-focus? this)
        .onFocusChange #(handle-focus-update this %)
        .autofocus (.-autofocus widget)
        .child
        (w/GestureDetector.
         .onTapDown (when primary-enabled?
                      #(handle-tap-down this %))
         .onTapUp (when primary-enabled?
                    #(handle-tap-up this %))
         .onTap (when primary-enabled?
                  #(handle-tap this))
         .onTapCancel (when primary-enabled?
                        #(handle-tap-cancel this))
         .onDoubleTap (when (.-on-double-tap widget)
                        #(handle-double-tap this))
         .onDoubleTapCancel (when (.-on-double-tap widget)
                              #(clear-offsets this))
         .onLongPressCancel (when (.-on-long-press widget)
                              #(clear-offsets this))
         .onLongPress (when (.-on-long-press widget)
                        #(handle-long-press this))
         .onLongPressStart (when (.-on-long-press widget)
                             #(handle-long-press-start this %))
         .onLongPressUp (when (.-on-long-press widget)
                          #(clear-offsets this))
         .onSecondaryTapDown (when secondary-enabled?
                               #(handle-secondary-tap-down this %))
         .onSecondaryTapUp (when secondary-enabled?
                             #(handle-secondary-tap-up this %))
         .onSecondaryTap (when secondary-enabled?
                           #(handle-secondary-tap this))
         .onSecondaryTapCancel (when secondary-enabled?
                                 #(handle-secondary-tap-cancel this))
         .behavior w/HitTestBehavior.opaque
         .excludeFromSemantics true
         .child
         (w/ValueListenableBuilder.
          .valueListenable (states-controller this)
          .builder
          (fn [ctx states _]
            (let [^w/MouseCursor effective-mouse-cursor
                  (w/WidgetStateProperty.resolveAs
                   (or (.-mouse-cursor widget)
                       w/WidgetStateMouseCursor.clickable)
                   states)]
              (w/MouseRegion.
               .cursor effective-mouse-cursor
               .onEnter #(handle-mouse-enter this %)
               .onExit #(handle-mouse-exit this %)
               .onHover #(handle-mouse-move this %)
               .child
               (w/DefaultSelectionStyle.merge
                .mouseCursor effective-mouse-cursor
                .child
                (w/Semantics.
                 .onTap (when-not (or (.-exclude-from-semantics widget)
                                      (nil? (.-on-tap widget)))
                          #(handle-tap this))
                 .onLongPress (when-not (or (.-exclude-from-semantics widget)
                                            (nil? (.-on-long-press widget)))
                                #(handle-long-press this))
                 .child
                 ((.-child widget)
                  {:context ctx
                   :state states
                   :prev-state (.-oldStates this)
                   :local-offset (.-cursorLocalOffset this)
                   :global-offset (.-cursorGlobalOffset this)}))))))))))))

  ^:mixin #/(w/AutomaticKeepAliveClientMixin Button)
  (wantKeepAlive [_] false))

(defn- ^w/Widget opacity-builder [opacity child]
  (w/Opacity .opacity opacity .child child))

(defn- default-button-modifier [button {:keys [state context prev-state]}]
  (let [text-style (w/DefaultTextStyle.of context)
        color (some-> text-style .-style .-color)
        alpha (some-> color .-a)
        did-unhovered (and (not (state :hovered)) (prev-state :hovered))
        did #(and (not (prev-state %)) (state %))]
    (w/DefaultTextStyle.merge
     .style (when color
              (w/TextStyle .color (.withOpacity color (if (state :disabled) (/ alpha 2.0) alpha))))
     .child (an/animated*
             {:duration (core/Duration .milliseconds (if did-unhovered 500 50))
              :update-strategy (if (or (did :pressed) (did :hovered)) :immediate :on-complete)}
             [opacity-builder
              (cond (state :pressed) 0.5 (state :hovered) 0.8 :else 1.0)
              button]))))

(def button-modifier
  {:key :button-modifier :default-value default-button-modifier})

(defn ^w/Widget button*
  ([args child]
   (let [[args on-tap] (cond
                         (map? args) [(assoc args :on-tap nil) (:on-tap args)]
                         (ifn? args) [{} args]
                         :else (throw (Exception. (str "Invalid button arguments: " args ", expected a map or a function."))))]
     (button* on-tap args child)))
  ([on-tap args child]
   (bd/builder*
    (fn [ctx]
      (Button
       (t/widget-key (args :key))
       (fn [btn-ctx]
         (let [ctx (:context btn-ctx ctx)
               label (wi/with-inherited* inh/button-context btn-ctx child)]
           (if-let [modifier (c/get-inherited ctx button-modifier)]
             (modifier label btn-ctx)
             label)))
       (c/get-inherited ctx inh/enabled (:enabled args))
       on-tap
       (:on-tap-down args)
       (:on-tap-up args)
       (:on-tap-cancel args)
       (:on-double-tap args)
       (:on-long-press args)
       (:on-secondary-tap args)
       (:on-secondary-tap-up args)
       (:on-secondary-tap-down args)
       (:on-secondary-tap-cancel args)
       (:on-highlight-changed args)
       (:on-hover args)
       (:mouse-cursor args)
       (c/get-inherited ctx inh/enable-feedback (:enable-feedback args))
       (c/get-inherited ctx inh/exclude-from-semantics (:exclude-from-semantics args))
       (:focus-node args)
       (c/get-inherited ctx inh/can-request-focus (:can-request-focus args))
       (:on-focus-change args)
       (c/get-inherited ctx inh/autofocus (:autofocus args)))))))

(defn ^w/Widget with-button-modifier*
  [modifier child]
  (wi/with-inherited* button-modifier modifier child))

(defn- no-styled-button [button _] button)

(defn ^w/Widget without-button-modifier*
  "A widget that removes the button modifier from its descendants."
  [child]
  (with-button-modifier* no-styled-button child))
